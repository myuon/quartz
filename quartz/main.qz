import quartz::std;
import quartz::path;
import quartz::lexer;
import quartz::parser;
import quartz::ast;
import quartz::ir;
import quartz::ir_code_gen;
import quartz::generator;
import quartz::typecheck;
import quartz::value;
import quartz::compiler;
import quartz::location;
import quartz::formatter;
import quartz::json;
import quartz::errors;
import quartz::preprocessor;

struct ArgCommand {
    command: string,
    args: vec[string],
    options: map[string, string],
}

struct ArgParser {
}

module ArgParser {
    fun parse(args: vec[string]): ArgCommand {
        let command = args.at(0);
        let command_args = make[vec[string]]();
        let options = make[map[string, string]]();

        let i = 1;
        while i < args.length {
            let arg = args.at(i);
            if arg.starts_with("--") {
                options.insert(arg.slice_start(2), args.at(i + 1));
                i = i + 1;
            } else {
                command_args.push(arg);
            }

            i = i + 1;
        }

        return ArgCommand {
            command: command,
            args: command_args,
            options: options,
        };
    }
}

enum CliCommand {
    compile: struct {
        output: string?,
        stdin: bool?,
        input: string?,
        entrypoint: string?,
        validate_address: bool?,
        test: bool,
    },
    format: struct {
        input: string?,
        stdin: bool?,
        write: bool?,
    },
    check: struct {
        input: string?,
    },
    check_type: struct {
        input: string?,
        line: i32,
        column: i32,
    },
    go_to_def: struct {
        input: string?,
        project: string?,
        line: i32,
        column: i32,
    },
    completion: struct {
        input: string?,
        line: i32,
        column: i32,
        stdin: bool,
        dot: bool,
    },
    version: struct {
    },
}

module CliCommand {
    fun parse(args: vec[string]): CliCommand or error {
        if args.at(0).equal("compile") {
            let i = 1;
            let compile = struct {
                output: nil,
                stdin: nil,
                input: nil,
                entrypoint: nil,
                validate_address: nil,
                test: false,
            };

            while i < args.length {
                if args.at(i).equal("-o") {
                    compile.output = args.at(i + 1)?;
                    i = i + 1;
                } else if args.at(i).equal("--output") {
                    compile.output = args.at(i + 1)?;
                    i = i + 1;
                } else if args.at(i).equal("--stdin") {
                    compile.stdin = true?;
                } else if args.at(i).equal("--entrypoint") {
                    compile.entrypoint = args.at(i + 1)?;
                    i = i + 1;
                } else if args.at(i).equal("--validate-address") {
                    compile.validate_address = true?;
                } else if args.at(i).equal("--test") {
                    compile.test = true;
                } else {
                    break;
                }

                i = i + 1;
            }

            if compile.stdin == nil {
                compile.input = args.at(i)?;
            }

            return CliCommand {
                compile: compile,
            };
        } else if args.at(0).equal("format") {
            let i = 1;
            let format_ = struct {
                input: nil,
                stdin: nil,
                write: nil,
            };

            while i < args.length {
                if args.at(i).equal("-w") {
                    format_.write = true?;
                } else if args.at(i).equal("--write") {
                    format_.write = true?;
                } else if args.at(i).equal("--stdin") {
                    format_.stdin = true?;
                } else {
                    break;
                }

                i = i + 1;
            }

            if format_.stdin == nil {
                format_.input = args.at(i)?;
            }

            return CliCommand {
                format: format_,
            };
        } else if args.at(0).equal("check") {
            let i = 1;
            let check = struct {
                input: nil,
            };

            check.input = args.at(i)?;

            return CliCommand {
                check: check,
            };
        } else if args.at(0).equal("check-type") {
            let i = 1;
            let check_type = struct {
                input: nil,
                line: 0,
                column: 0,
            };

            while i < args.length {
                if args.at(i).equal("--line") {
                    check_type.line = i32::parse(args.at(i + 1));
                    i = i + 1;
                } else if args.at(i).equal("--column") {
                    check_type.column = i32::parse(args.at(i + 1));
                    i = i + 1;
                } else if args.at(i).equal("--project") {
                    i = i + 1;
                } else {
                    check_type.input = args.at(i)?;
                }

                i = i + 1;
            }

            return CliCommand {
                check_type: check_type,
            };
        } else if args.at(0).equal("go-to-def") {
            let i = 1;
            let go_to_def = struct {
                input: nil,
                project: nil,
                line: 0,
                column: 0,
            };

            while i < args.length {
                if args.at(i).equal("--line") {
                    go_to_def.line = i32::parse(args.at(i + 1));
                    i = i + 1;
                } else if args.at(i).equal("--column") {
                    go_to_def.column = i32::parse(args.at(i + 1));
                    i = i + 1;
                } else if args.at(i).equal("--project") {
                    go_to_def.project = args.at(i + 1)?;
                    i = i + 1;
                } else {
                    go_to_def.input = args.at(i)?;
                }

                i = i + 1;
            }

            return CliCommand {
                go_to_def: go_to_def,
            };
        } else if args.at(0).equal("completion") {
            let i = 1;
            let completion = struct {
                input: nil,
                line: 0,
                column: 0,
                stdin: false,
                dot: false,
            };

            while i < args.length {
                if args.at(i).equal("--line") {
                    completion.line = i32::parse(args.at(i + 1));
                    i = i + 1;
                } else if args.at(i).equal("--column") {
                    completion.column = i32::parse(args.at(i + 1));
                    i = i + 1;
                } else if args.at(i).equal("--stdin") {
                    completion.stdin = true;
                } else if args.at(i).equal("--dot") {
                    completion.dot = true;
                } else if args.at(i).equal("--project") {
                    i = i + 1;
                } else {
                    completion.input = args.at(i)?;
                }

                i = i + 1;
            }

            return CliCommand {
                completion: completion,
            };
        } else if args.at(0).equal("version") {
            return CliCommand {
                version: struct {
                },
            };
        } else {
            return _ or error::new(format("unknown command: {}", derive::to_string(args)));
        }
    }
}

@[test]
fun test_parse() {
    let command or err = CliCommand::parse(make[vec[string]](
        "completion",
        "--project",
        "/tmp/",
        "--line",
        "0",
        "--column",
        "2",
        "/tmp/foobar",
    ));
    assert(err == nil);
    assert_eq(
        command!,
        CliCommand {
            completion: struct {
                input: "/tmp/foobar"?,
                line: 0,
                column: 2,
                stdin: false,
                dot: false,
            },
        },
    );

    let command or err = CliCommand::parse(make[vec[string]](
        "completion",
        "--project",
        "/tmp/",
        "--line",
        "0",
        "--column",
        "2",
        "--stdin",
    ));
    assert(err == nil);
    assert_eq(
        command!,
        CliCommand {
            completion: struct {
                input: nil,
                line: 0,
                column: 2,
                stdin: true,
                dot: false,
            },
        },
    );
}

fun start(): nil or error {
    let compiler_version = "3.1.2";

    let args = args();
    let command or err = CliCommand::parse(args.slice(1, args.length));
    if err != nil {
        panic(err!.message);
    }

    let envs = environs();

    let lib_path = nil as string?;
    let env_QUARTZ_LIB_PATH = "QUARTZ_LIB_PATH";
    if envs.has(env_QUARTZ_LIB_PATH) {
        lib_path = envs.at(env_QUARTZ_LIB_PATH)?;
    }

    let compiler = Compiler::new(lib_path, compiler_version);

    let command = command!;
    if command.compile != nil {
        let compile = command.compile!;

        let input = "";
        if compile.input != nil {
            input = file_read(compile.input!).try;
        } else if compile.stdin != nil {
            input = stdin();
        } else {
            panic("no input");
        }

        let path = Path::new("main");

        let entrypoint = "main";
        if compile.entrypoint != nil {
            entrypoint = compile.entrypoint!;
        }

        let validate_address = false;
        if compile.validate_address != nil {
            validate_address = compile.validate_address!;
        }

        let result or err = compiler.compile(
            input,
            path,
            entrypoint,
            false,
            validate_address,
            compile.test,
            compile.output!,
            envs.has("VERBOSE"),
        );
        if err != nil {
            err_println(err!.message);

            return nil;
        }

        println("compiled: writing wat to {}", compile.output!);

        return nil;
    } else if command.format != nil {
        let f = command.format!;

        let input = "";
        if f.input != nil {
            input = file_read(f.input!).try;
        } else if f.stdin != nil {
            input = stdin();
        } else {
            panic("no input");
        }

        let result or err = run_format(input);
        if err != nil {
            err_println(err!.message);
            return;
        }

        if f.write != nil {
            file_write(f.input!, result!);
        } else {
            println("{}", result!);
        }
    } else if command.check != nil {
        let check = command.check!;

        let input = file_read(check.input!).try;

        let path = Path::new("main");

        let errs = compiler.check(input, path);

        let result = make[vec[Json]]();
        for err in errs {
            let message = make[map[string, Json]]();
            message.insert(
                "message",
                Json {
                    t_string: err.message,
                },
            );
            message.insert(
                "start",
                Json {
                    t_array: make[vec[Json]](
                        Json {
                            t_i32: err.start.line,
                        },
                        Json {
                            t_i32: err.start.column,
                        },
                    ),
                },
            );
            message.insert(
                "end",
                Json {
                    t_array: make[vec[Json]](
                        Json {
                            t_i32: err.end.line,
                        },
                        Json {
                            t_i32: err.end.column,
                        },
                    ),
                },
            );

            result.push(Json {
                t_object: message,
            });
        }

        println(Json {
            t_array: result,
        }.to_string());
    } else if command.check_type != nil {
        let check_type = command.check_type!;

        let input = file_read(check_type.input!).try;

        let result or err = compiler.check_type(check_type.input!, input, check_type.line, check_type.column);
        if err != nil {
            err_println(err!.message);
            return;
        }

        if result != nil {
            println(result!);
        }
    } else if command.go_to_def != nil {
        let go_to_def = command.go_to_def!;

        let path_segments = go_to_def.input!.split(".").at(0).split("/");
        let path = Path::new(
            path_segments.at(path_segments.length - 2),
            path_segments.at(path_segments.length - 1),
        );

        let input = file_read(go_to_def.input!).try;

        let result or err = compiler.go_to_def(input, path, go_to_def.line, go_to_def.column);
        if err != nil {
            err_println(err!.message);
            return;
        }

        if result != nil {
            let items = make[map[string, Json]]();
            items.insert(
                "file",
                Json {
                    t_string: format("{}/{}.qz", go_to_def.project!, result!.module_name),
                },
            );

            let start = make[map[string, Json]]();
            start.insert(
                "line",
                Json {
                    t_i32: result!.start.line,
                },
            );
            start.insert(
                "column",
                Json {
                    t_i32: result!.start.column,
                },
            );

            let end = make[map[string, Json]]();
            end.insert(
                "line",
                Json {
                    t_i32: result!.end.line,
                },
            );
            end.insert(
                "column",
                Json {
                    t_i32: result!.end.column,
                },
            );

            items.insert(
                "start",
                Json {
                    t_object: start,
                },
            );
            items.insert(
                "end",
                Json {
                    t_object: end,
                },
            );

            println(Json {
                t_object: items,
            }.to_string());
        }
    } else if command.completion != nil {
        let completion = command.completion!;

        let input = "";
        if completion.stdin {
            input = stdin();
        } else if completion.input != nil {
            input = file_read(completion.input!).try;
        } else {
            panic("no input");
        }

        let result or err = compiler.completion(
            input,
            Path::new("main"),
            completion.line,
            completion.column,
            completion.dot,
        );
        if err != nil {
            err_println(err!.message);
            return;
        }

        if result != nil {
            let items = make[vec[Json]]();
            for r in result! {
                let item = make[map[string, Json]]();
                item.insert(
                    "kind",
                    Json {
                        t_string: r.kind,
                    },
                );
                item.insert(
                    "label",
                    Json {
                        t_string: r.label,
                    },
                );
                item.insert(
                    "detail",
                    Json {
                        t_string: r.text,
                    },
                );

                items.push(Json {
                    t_object: item,
                });
            }

            let container = make[map[string, Json]]();
            container.insert(
                "items",
                Json {
                    t_array: items,
                },
            );

            println(Json {
                t_object: container,
            }.to_string());
        }
    } else if command.version != nil {
        println("quartz version {}", compiler_version);
    } else {
        println("unknown args: {}", derive::to_string(args));
        println("{}", args.at(1));

        return nil;
    }

    return nil;
}

fun run_format(input_: string): string or error {
    let lexer = Lexer::new();
    lexer.run(input_).try;

    let comments = make[vec[Token]]();
    for token in lexer.tokens {
        if token.lexeme.equal("comment") {
            comments.push(token);
        }
    }

    let parser = Parser::new();
    let module_ = parser.run(lexer.tokens, false).try;

    let formatter = Formatter::new(input_, comments, 0);

    return formatter.format(module_);
}

fun main() {
    let result or err = start();
    if err != nil {
        panic("{}", err!.message);
    }
}
