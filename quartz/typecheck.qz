import quartz::ast;

type Typechecker = {
    locals: map[string, Type],
    globals: map[string, Type],
};

module Typechecker {
    fun new(): Typechecker {
        return Typechecker {
            locals: make[map[string, Type]](),
            globals: make[map[string, Type]](),
        };
    }

    fun module_(self, m: Module) {
        for i in 0..m.decls.length {
            let d = m.decls.at(i);
            self.decl(d);
        }
    }

    fun decl(self, d: Decl) {
        if d.t_func != nil {
            // For recursive functions
            self.globals.insert(d.t_func!.name, d.t_func!.to_type());

            self.function(d.t_func!);

            self.globals.insert(d.t_func!.name, d.t_func!.to_type());
            return;
        }
        if d.t_let != nil {
            let t = self.expression(d.t_let!.expr);
            unify(t, d.t_let!.name_type);
            d.t_let!.name_type = t;

            self.globals.insert(d.t_let!.name, t);
            return;
        }

        panic("unimplemented: decl");
    }

    fun function(self, f: Function) {
        let locals = self.locals;
        self.locals = make[map[string, Type]]();

        for i in 0..f.params.length {
            let p = f.params.at(i);
            self.locals.insert(p.name, p.type_);
        }

        self.block(f.body, f.result_type);

        self.locals = locals;
    }

    fun block(self, b: Block, expected: Type) {
        for i in 0..b.block.length {
            let result = Type {
                t_omit: true?,
                ..nil,
            };
            self.statement(b.block.at(i), result);
        }
    }

    fun statement(self, s: Statement, result_type: Type) {
        if s.t_let != nil {
            let t = self.expression(s.t_let!.expr);
            unify(t, s.t_let!.name_type);
            s.t_let!.name_type = t;

            self.locals.insert(s.t_let!.name, t);
            return;
        }
        if s.t_return != nil {
            let t = self.expression(s.t_return!.expr);
            unify(t, result_type);
            return;
        }
        if s.t_expr != nil {
            self.expression(s.t_expr!.expr);
            return;
        }
        if s.t_assign != nil {
            let lhs_type = self.expression_left_value(s.t_assign!.lhs);
            let rhs_type = Type {
                t_ptr: self.expression(s.t_assign!.rhs)?,
                ..nil,
            };

            unify(lhs_type, rhs_type);
            return;
        }
        if s.t_if != nil {
            let condition_type = self.expression(s.t_if!.condition);
            unify(condition_type, Type {
                t_bool: true?,
                ..nil,
            });

            let type_ = Type {
                t_omit: true?,
                ..nil,
            };
            self.block(s.t_if!.then_block, type_);
            if s.t_if!.else_block != nil {
                self.block(s.t_if!.else_block!, type_);
            }
            return;
        }

        println(s.to_string());
        panic("unimplemented: statement");
    }

    fun expression_left_value(self, expr: Expression): Type {
        let t = self.expression(expr);

        return Type {
            t_ptr: t?,
            ..nil,
        };
    }

    fun expression(self, expr: Expression): Type {
        if expr.t_number != nil {
            return Type {
                t_i32: true?,
                ..nil,
            };
        }
        if expr.t_binop != nil {
            if (expr.t_binop!.binop.equal("plus")
                || expr.t_binop!.binop.equal("minus")
                || expr.t_binop!.binop.equal("mult")) {
                let t1 = self.expression(expr.t_binop!.left);
                unify(t1, Type {
                    t_i32: true?,
                    ..nil,
                });

                let t2 = self.expression(expr.t_binop!.right);
                unify(t2, Type {
                    t_i32: true?,
                    ..nil,
                });

                return t1;
            }
            if (expr.t_binop!.binop.equal("equal")) {
                let t1 = self.expression(expr.t_binop!.left);
                let t2 = self.expression(expr.t_binop!.right);
                unify(t1, t2);

                return Type {
                    t_bool: true?,
                    ..nil,
                };
            }

            println(expr.t_binop!.binop);
            panic("unimplemented: binop");
        }
        if expr.t_ident != nil {
            if self.globals.has(expr.t_ident!.name) {
                let t = self.globals.at(expr.t_ident!.name);

                return t;
            }
            if self.locals.has(expr.t_ident!.name) {
                let t = self.locals.at(expr.t_ident!.name);

                return t;
            }

            panic("unbound identifier: ".concat(expr.t_ident!.name));
        }
        if expr.t_call != nil {
            let func_type = self.expression(expr.t_call!.callee).t_func!;
            if func_type.params.length != expr.t_call!.args.length {
                println(expr.to_string());
                println(func_type.params.length.to_string().concat(" != ").concat(expr.t_call!.args.length.to_string()));
                panic("wrong number of arguments");
            }

            for i in 0..expr.t_call!.args.length {
                let arg = expr.t_call!.args.at(i);
                let arg_type = self.expression(arg);
                let param_type = func_type.params.at(i);

                unify(arg_type, param_type);
            }

            return func_type.result;
        }

        panic("unimplemented: expression");
    }
}

fun unify(type1: Type, type2: Type) {
    if type1.t_i32 != nil && type2.t_i32 != nil {
        return;
    }
    if type1.t_nil != nil && type2.t_nil != nil {
        return;
    }
    if type1.t_omit != nil {
        return;
    }
    if type2.t_omit != nil {
        return;
    }
    if type1.t_ptr != nil && type2.t_ptr != nil {
        unify(type1.t_ptr!, type2.t_ptr!);
        return;
    }
    if type1.t_bool != nil && type2.t_bool != nil {
        return;
    }

    panic("unification failed: ".concat(type1.to_string()).concat(" != ").concat(type2.to_string()));
}
