import quartz::lexer;

type Parser = {
    position: i32,
    input: vec[Token],
    omit_index: i32,
};

type Function = {
    name: string,
    result_type: string,
    body: Block,
};

type Block = {
    block: vec[Statement],
};

type Statement = {
    t: string,
    name: string?,
    name_type: string?,
    expr: Expression,
};

type Expression = {
    t: string,
    left: Expression?,
    right: Expression?,
    value: string?,
    operator: string?,
};

module Parser {
    fun new(): Parser {
        return Parser {
            position: 0,
            input: make[vec[Token]](),
            omit_index: 0,
        };
    }

    fun run(self, input: vec[Token]) {
        self.input = input;
        return self.module_();
    }

    fun module_(self) {
        self.decl();
    }

    fun decl(self) {
        self.expect("fun");
        let name = self.ident();
        self.expect("lparen");
        self.expect("rparen");

        self.expect("colon");
        let result_type = self.ident();

        self.expect("lbrace");
        let body = self.block();
        self.expect("rbrace");

        return Function {
            name: name,
            result_type: result_type,
            body: body,
        };
    }

    fun function(self) {
        self.consume();
    }

    fun peek(self): Token {
        return self.input.at(self.position);
    }

    fun is_end(self): bool {
        return self.position >= self.input.length;
    }

    fun consume(self): Token {
        let token = self.peek();
        self.position = self.position + 1;

        return token;
    }

    fun ident(self): string {
        let token = self.consume();
        if token.lexeme.equal("ident") {
            return token.string_value!;
        } else {
            panic("Expected IDENT, got ".concat(token.lexeme).concat("{").concat(token.string_value!).concat("}"));
        }
    }

    fun expect(self, lexeme: string) {
        let token = self.consume();
        if token.lexeme.equal(lexeme) {
            return token;
        } else {
            panic("Expected ".concat(lexeme).concat(", got ").concat(token.lexeme).concat("{").concat(token.string_value!).concat("}"));
        }
    }

    fun expect_ident(self, expected: string) {
        let i = self.ident();
        if !i.equal(expected) {
            panic("Expected ".concat(expected).concat(", got ").concat(i));
        }
    }

    fun block(self) {
        let statements = make[vec[Statement]]();

        while !self.is_end() && !self.peek().lexeme.equal("rbrace") {
            statements.push(self.statement());
        }

        return Block {
            block: statements,
        };
    }

    fun statement(self) {
        let token = self.peek();
        if token.lexeme.equal("let") {
            return self.let_statement();
        } else {
            if token.lexeme.equal("return") {
                return self.return_statement();
            } else {
                panic("Unexpected token ".concat(token.lexeme).concat("{").concat(token.string_value!).concat("}"));
            }
        }
    }

    fun let_statement(self): Statement {
        self.consume();
        let name = self.ident();
        self.expect("colon");
        let t = self.ident();
        self.expect("equal");
        let value = self.expression();
        self.expect("semicolon");

        return Statement {
            t: "let",
            name: name?,
            name_type: t?,
            expr: value,
        };
    }

    fun return_statement(self): Statement {
        self.consume();
        let value = self.expression();
        self.expect("semicolon");

        return Statement {
            t: "return",
            name: nil,
            name_type: nil,
            expr: value,
        };
    }

    fun expression(self): Expression {
        let token = self.peek();

        if token.lexeme.equal("number") {
            return Expression {
                t: "number",
                value: token.string_value,
                left: nil,
                right: nil,
                operator: nil,
            };
        }

        panic("Unexpected token ".concat(token.lexeme).concat("{").concat(token.string_value!).concat("}"));
    }
}
