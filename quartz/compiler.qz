import quartz::lexer;
import quartz::parser;
import quartz::typecheck;
import quartz::ir_code_gen;
import quartz::generator;
import quartz::path;
import quartz::ast;
import quartz::ir;
import quartz::value;
import quartz::location;

struct LoadedModule {
    path: Path,
    source: string,
    module_: Module,
}

struct Compiler {
    loaded: vec[LoadedModule],
}

module Compiler {
    fun new(): Compiler {
        return Compiler {
            loaded: make[vec[LoadedModule]](),
        };
    }

    fun load_module(self, path: Path, skip_errors: bool): vec[Path] or error {
        let input = file_read(format("{}.qz", path.join("/")));

        return self.register_module(path, input, skip_errors).try;
    }

    fun matches(self, path: Path): LoadedModule? {
        for loaded in self.loaded {
            if is_prefix_path(path, loaded.path) {
                return loaded?;
            }
        }

        return nil;
    }

    fun register_module(self, path: Path, input: string, skip_errors: bool): vec[Path] or error {
        let lexer = Lexer::new();
        lexer.run(input).try;

        let parser = Parser::new();
        let module_ = parser.run(lexer.tokens, skip_errors).try;

        self.loaded.push(LoadedModule {
            path: path,
            source: input,
            module_: module_,
        });

        return parser.imports;
    }

    fun parse(self, input: string, load_path: Path?, skip_errors: bool): Module or error {
        let main_path = Path::new("main");
        if load_path != nil {
            main_path = load_path!;
        }
        let imports = self.register_module(main_path, input, skip_errors).try;

        let visited = make[map[string, bool]]();
        visited.insert(main_path.join("_"), true);
        imports.push(Path::new("quartz", "std"));

        while imports.length != 0 {
            let path = vec_pop(imports) as Path;
            if visited.has(path.join("_")) {
                continue;
            }

            imports.extend(self.load_module(path, skip_errors).try);
            visited.insert(path.join("_"), true);
        }

        let decls = make[vec[LDecl]]();
        for loaded in self.loaded {
            decls.push(LDecl {
                data: Decl {
                    t_module: struct {
                        path: loaded.path,
                        module_: loaded.module_,
                    },
                },
                location: Location::unknown(),
            });
        }

        let module_ = Module {
            decls: decls,
        };

        return module_;
    }

    fun compile(
        self,
        input: string,
        main_path: Path,
        entrypoint: string,
        skip_errors: bool,
    ): string or error {
        let module_ = self.parse(input, nil, skip_errors).try;

        let typechecker = Typechecker::new();
        typechecker.run(module_, main_path).try;

        let irgen = IrCodeGenerator::new();
        irgen.globals = typechecker.globals;
        let term = irgen.run(module_).try;

        let gen = Generator::new();
        gen.set_globals(typechecker.globals);
        gen.set_strings(irgen.strings.strings);
        gen.set_type_reps(irgen.type_reps.type_reps);
        gen.set_start_terms(irgen.start);

        term = gen.fold_consts(term);

        let code = gen.run(term, entrypoint, irgen.data_section_offset);

        return code;
    }

    fun check(self, input: string, path: Path): nil or error {
        let module_ = self.parse(input, nil, true).try;

        let typechecker = Typechecker::new();
        typechecker.run(module_, path).try;

        return nil;
    }

    fun check_type(self, filepath: string, input: string, line: i32, column: i32): string or error {
        let path = Path::new("main");

        let module_ = self.parse(input, nil, true).try;

        let position = find_line_column_from_position(input, line, column);

        let typechecker = Typechecker::new();
        let t = typechecker.find_at_cursor(module_, path, position).try;

        return format("```quartz\n{}\n```", t.to_string());
    }

    fun completion(
        self,
        input: string,
        path: Path,
        line: i32,
        column: i32,
        dot: bool,
    ): vec[struct {
        kind: string,
        label: string,
        text: string,
    }] or error {
        let module_ = self.parse(input, nil, true).try;
        let position = find_line_column_from_position(input, line, column);

        let typechecker = Typechecker::new();
        let result = typechecker.completion(module_, path, position, dot).try;

        return result;
    }

    fun go_to_def(
        self,
        input: string,
        path: Path,
        line: i32,
        column: i32,
    ): struct {
        module_name: string,
        start: struct {
            line: i32,
            column: i32,
        },
        end: struct {
            line: i32,
            column: i32,
        },
    } or error {
        let module_ = self.parse(input, path?, true).try;
        let position = find_line_column_from_position(input, line, column);

        let typechecker = Typechecker::new();
        let result = typechecker.find_definition(module_, path, position).try;

        let loaded = self.matches(path);
        if loaded != nil {
            return struct {
                module_name: loaded!.path.join("/"),
                start: find_position(loaded!.source, result.start),
                end: find_position(loaded!.source, result.end),
            };
        } else {
            return _ or error::new("module not found");
        }
    }
}

fun find_position(
    input: string,
    position: i32,
): struct {
    line: i32,
    column: i32,
} {
    let line_number = 0;
    let count = 0;
    for line in input.line_counts() {
        if count + line > position {
            break;
        }

        line_number = line_number + 1;
        count = count + line + 1;
    }

    return struct {
        line: line_number,
        column: position - count,
    };
}

fun find_line_column_from_position(input: string, line: i32, column: i32): i32 {
    let start = 0;
    let end = input.length;
    let line_counts = input.line_counts();

    while start < end {
        let mid = (start + end) / 2;
        let pos = find_position_by_line_counts(line_counts, mid);

        if pos.line == line && pos.column == column {
            return mid;
        }

        if pos.line < line || (pos.line == line && pos.column < column) {
            start = mid + 1;
        } else {
            end = mid;
        }
    }

    return start;
}

fun find_position_by_line_counts(
    line_counts: vec[i32],
    position: i32,
): struct {
    line: i32,
    column: i32,
} {
    let line_number = 0;
    let count = 0;
    for line in line_counts {
        if count + line > position {
            break;
        }

        line_number = line_number + 1;
        count = count + line + 1;
    }

    return struct {
        line: line_number,
        column: position - count,
    };
}

