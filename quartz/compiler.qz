import quartz::lexer;
import quartz::parser;
import quartz::typecheck;
import quartz::ir_code_gen;
import quartz::generator;
import quartz::path;
import quartz::ast;
import quartz::ir;
import quartz::value;

struct LoadedModule {
    path: Path,
    source: string,
    module_: Module,
}

struct Compiler {
    loaded: vec[LoadedModule],
}

module Compiler {
    fun new(): Compiler {
        return Compiler {
            loaded: make[vec[LoadedModule]](),
        };
    }

    fun load_module(self, path: Path): vec[Path] or error {
        let input = file_read(format("{}.qz", path.join("/")));

        return self.register_module(path, input).try;
    }

    fun register_module(self, path: Path, input: string): vec[Path] or error {
        let lexer = Lexer::new();
        lexer.run(input).try;

        let parser = Parser::new();
        let module_ = parser.run(lexer.tokens).try;

        self.loaded.push(LoadedModule {
            path: path,
            source: input,
            module_: module_,
        });

        return parser.imports;
    }

    fun compile(self, input: string): string or error {
        let main_path = Path::new("main");
        let imports = self.register_module(main_path, input).try;

        let visited = make[map[string, bool]]();
        visited.insert(main_path.join("_"), true);
        imports.push(Path::new("quartz", "std"));

        while imports.length != 0 {
            let path = vec_pop(imports) as Path;
            if visited.has(path.join("_")) {
                continue;
            }

            imports.extend(self.load_module(path).try);
            visited.insert(path.join("_"), true);
        }
        // println("loaded modules");
        // debug(alloc_ptr);

        let decls = make[vec[Decl]]();
        for loaded in self.loaded {
            decls.push(Decl {
                t_module: struct {
                    path: loaded.path,
                    module_: loaded.module_,
                },
            });
        }

        let module_ = Module {
            decls: decls,
        };

        let typechecker = Typechecker::new();
        typechecker.module_(module_).try;
        // println("typecheck");
        // debug(alloc_ptr);

        let irgen = IrCodeGenerator::new();
        irgen.globals = typechecker.globals;
        let term = irgen.run(module_).try;
        // println("irgen");
        // debug(alloc_ptr);

        let gen = Generator::new();
        gen.set_globals(typechecker.globals);
        gen.set_strings(irgen.strings.strings);

        gen.fold_consts(term);

        let code = gen.run(term, irgen.data_section_offset);

        return code;
    }
}

