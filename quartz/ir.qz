import quartz::ast;

type IrTerm = {
    t_nil: bool?,
    t_i32: string?,
    t_ident: string?,
    t_func: IrFunc?,
    t_call: IrCall?,
    t_let: IrLet?,
    t_return: IrReturn?,
    t_module: struct {
        elements: vec[IrTerm],
    }?,
    t_global_let: struct {
        name: string,
        type_: IrType,
        value: IrTerm,
    }?,
    t_assign: struct {
        lhs: string,
        rhs: IrTerm,
    }?,
    t_if: struct {
        condition: IrTerm,
        then_term: IrTerm,
        else_term: IrTerm,
    }?,
    t_while: struct {
        condition: IrTerm,
        block: IrTerm,
        cleanup: IrTerm?,
    }?,
    t_seq: struct {
        terms: vec[IrTerm],
    }?,
    t_store: struct {
        type_: IrType,
        address: IrTerm,
        offset: IrTerm,
        value: IrTerm,
    }?,
    t_load: struct {
        type_: IrType,
        address: IrTerm,
        offset: IrTerm,
    }?,
    t_sizeof: struct {
        type_: IrType,
    }?,
};

module IrTerm {
    fun to_string(self): string {
        return derive::to_string_pretty(self);
    }

    fun ident(name: string): IrTerm {
        return IrTerm{
            t_ident: name?,
            ..nil,
        };
    }

    fun i32(value: i32): IrTerm {
        return IrTerm{
            t_i32: value.to_string()?,
            ..nil,
        };
    }

    fun find_locals(self): vec[string] {
        let locals = make[vec[string]]();

        if self.t_let != nil {
            locals.push(self.t_let!.name);
            locals.extend(self.t_let!.value.find_locals());
        }
        if self.t_call != nil {
            locals.extend(self.t_call!.callee.find_locals());
            for arg in self.t_call!.args {
                locals.extend(arg.find_locals());
            }
        }
        if self.t_seq != nil {
            for term in self.t_seq!.terms {
                locals.extend(term.find_locals());
            }
        }
        if self.t_if != nil {
            locals.extend(self.t_if!.condition.find_locals());
            locals.extend(self.t_if!.then_term.find_locals());
            locals.extend(self.t_if!.else_term.find_locals());
        }
        if self.t_while != nil {
            locals.extend(self.t_while!.condition.find_locals());
            locals.extend(self.t_while!.block.find_locals());
            if self.t_while!.cleanup != nil {
                locals.extend(self.t_while!.cleanup!.find_locals());
            }
        }
        if self.t_store != nil {
            locals.extend(self.t_store!.address.find_locals());
            locals.extend(self.t_store!.offset.find_locals());
            locals.extend(self.t_store!.value.find_locals());
        }
        if self.t_load != nil {
            locals.extend(self.t_load!.address.find_locals());
            locals.extend(self.t_load!.offset.find_locals());
        }
        if self.t_assign != nil {
            locals.extend(self.t_assign!.rhs.find_locals());
        }
        if self.t_return != nil {
            locals.extend(self.t_return!.value.find_locals());
        }

        return locals;
    }
}

type IrFunc = {
    name: string,
    params: vec[struct {
        name: string,
        type_: IrType,
    }],
    body: vec[IrTerm],
    result_type: IrType,
    locals: vec[string],
};

type IrCall = {
    callee: IrTerm,
    args: vec[IrTerm],
};

type IrLet = {
    name: string,
    type_: IrType,
    value: IrTerm,
};

type IrReturn = {
    value: IrTerm,
};

type IrType = {
    t_nil: bool?,
    t_i32: bool?,
    t_address: bool?,
};

module IrType {
    fun new(t: Type): IrType {
        if t.t_nil != nil {
            return IrType{
                t_nil: true?,
                ..nil,
            };
        }
        if t.t_i32 != nil {
            return IrType{
                t_i32: true?,
                ..nil,
            };
        }

        return panic("unknown type: {}", t.to_string());
    }

    fun to_string(self): string {
        if self.t_nil != nil {
            return "nil";
        }
        if self.t_i32 != nil {
            return "i32";
        }

        return panic("unknown type");
    }

    fun is_nil(self): bool {
        return self.t_nil != nil;
    }

    fun sizeof(self): i32 {
        if self.t_nil != nil {
            return 4;
        }
        if self.t_i32 != nil {
            return 4;
        }
        if self.t_address != nil {
            return 4;
        }

        return panic("unknown type");
    }
}
